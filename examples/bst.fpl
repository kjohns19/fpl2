insert insptr nodeptr value cmpptr 4 fun
    if nodeptr deref none == then
        newnode heap =
        newnode deref obj =
        newnode deref "value" at value =
        newnode deref "left" at none =
        newnode deref "right" at none =
        nodeptr deref newnode =
        none return
    end
    node nodeptr deref =
    cmp value node deref "value" at cmpptr deref call =
    if cmp 0 < then
        insptr node deref "left" at ref value cmpptr insptr deref call
        none return
    end
    if cmp 0 > then
        insptr node deref "right" at ref value cmpptr insptr deref call
        none return
    end
end =

print_tree prnptr node 2 fun
    if node none != then
        prnptr node deref "left" at prnptr deref call
        node deref "value" at print
        prnptr node deref "right" at prnptr deref call
    end
end =

delete_tree delptr node 2 fun
    if node none != then
        delptr node deref "left" at delptr deref call
        delptr node deref "right" at delptr deref call
        node deref delete
    end
end =

cmpfunc a b 2 fun
    b a - return
end =

root none =

"Enter number of values to add:" print
size get num =
"Adding " size str + " values" + print
i 0 =
while i size < do
    value 0 100 rand =
    value print
    insert ref root ref value cmpfunc ref insert call
    i i 1 + =
end

"Printing tree" print
print_tree ref root print_tree call
delete_tree ref root delete_tree call
